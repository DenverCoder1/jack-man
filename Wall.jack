class Wall {
  // corners of wall
  field int leftX, topY, rightX, bottomY;

  // construct wall
  constructor Wall new(int x1, int y1, int x2, int y2) {
    let leftX = x1;
    let topY = y1;
    let rightX = x2;
    let bottomY = y2;
    return this;
  }

  // draw wall rectangle
  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(leftX, topY, rightX, bottomY);
    return;
  }

  // check if object is overlapping wall given its coordinates
  method boolean checkOverlapping(int x1, int y1, int x2, int y2) {
    return (
      // left of object overlaps right of wall
      // right of object overlaps left of wall
      (x1 < (rightX + 1)) & (x2 > (leftX - 1))
    ) & (
      // top of object overlaps bottom of wall
      // bottom of object overlaps top of wall
      (y1 < (bottomY + 1)) & (y2 > (topY - 1))
    );
  }

  // dispose wall
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}