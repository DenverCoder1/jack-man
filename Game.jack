class Game {
  // current score
  field Score score;
  // the pacman instance
  field PacMan pacman;
  // grid of pellets
  field Pellets pellets;
  // collection of walls
  field Walls walls;
  // collection of ghosts
  field Ghosts ghosts;
  // labels on the side excluding score, highscore
  field Labels labels;

  // create a new game
  constructor Game new() {
    // initialize sprites
    do createSprites();
    return this;
  }

  // call initialization functions
  method void init() {
    do pacman.init();
    do walls.init();
    do pellets.init();
    do score.init();
    do ghosts.init();
    do labels.init();
    return;
  }

  // repeated game logic
  method void next() {
    var int key;

    // if the game is over, write message and wait for F2
    if (pacman.getIsCaptured()) {
      do Output.moveCursor(10, 46); // row, col
      do Output.printString("GAME OVER");
      do Output.moveCursor(13, 46); // row, col
      do Output.printString("INSERT COIN");
      do Output.moveCursor(14, 46); // row, col
      do Output.printString("- PRESS F2");
      // wait for F2 to be pressed
	    while (true) {
		    let key = Keyboard.keyPressed();
		    if (key = 142) {
          // create a new game
			    do newGame();
			    return;
		    }
	    }
    }

    // refresh state of game objects
    do pacman.next();
    do ghosts.next();
    do score.next();
    return;
  }

  method void handleKey(int key) {
    // reset if F2 pressed
    if (key = 142) {
      // create a new game
      do newGame();
      return;
    }
    // handle as pacman movement
    do pacman.handleKey(key);
    return;
  }

	method void newGame() {
    // destroy sprites
    do disposeSprites();
    // clear screen
    do Screen.clearScreen();
    // recreate sprites
    do createSprites();
    // call initialization functions
    do init();
    return;
  }

  // create all sprites
  method void createSprites() {
    let walls = Walls.new();
    let pellets = Pellets.new();
    let pacman = PacMan.new(pellets, walls);
    let score = Score.new(pellets);
    let ghosts = Ghosts.new(pacman, walls, pellets);
    let labels = Labels.new();
    return;
  }

  // destroy all sprites
  method void disposeSprites() {
    do walls.dispose();
	  do pellets.dispose();
	  do pacman.dispose();
	  do score.dispose();
	  do ghosts.dispose();
    do labels.dispose();
    return;
  }

  // dispose all
  method void dispose() {
    do disposeSprites();
    do Memory.deAlloc(this);
    return;
  }
}