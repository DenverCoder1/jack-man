class Game {
  field Score score;
  field PacMan pacman;
  field Pellets pellets;
  field Walls walls;
  field Ghosts ghosts;
  field Labels labels;

  constructor Game new() {
	  do Random.setSeed(Random.randomSeed()); 
  
    let walls = Walls.new();
    let pellets = Pellets.new();
    let pacman = PacMan.new(pellets, walls);
    let score = Score.new(pellets);
    let ghosts = Ghosts.new(pacman, walls, pellets);
    let labels = Labels.new();
    return this;
  }

  method void init() {
    do pacman.init();
    do walls.init();
    do pellets.init();
    do score.init();
    do ghosts.init();
    do labels.init();
    return;
  }

  method void next() {
	var int key;
    if (isGameOver()) {
      do Output.moveCursor(10, 46); // row, col
      do Output.printString("GAME OVER");
      do Output.moveCursor(13, 46); // row, col
      do Output.printString("INSERT COIN");
      do Output.moveCursor(14, 46); // row, col
      do Output.printString("- PRESS F2");
	  
	  while (true) {
		
		let key = Keyboard.keyPressed();
		if (key = 142) {
			do newGame();
			return;
			
		}
	  }
	        
    }
    do pacman.next();
    do ghosts.next();
    do score.next();
    return;
  }

  method boolean isGameOver() {
    return pacman.getIsCaptured();
  }

  method void reset() {
    do pacman.reset();
    return;
  }

  method void handleKey(int key) {
    if (key = 81) {
      do reset();
      return;
    }
    do pacman.handleKey(key);
    return;
  }

	method void newGame() {
    do pellets.dispose();
    do pacman.dispose();
    do score.dispose();
    do ghosts.dispose();
    do labels.dispose();
    do walls.dispose();
    do Screen.clearScreen();
    
    let walls = Walls.new();
    let pellets = Pellets.new();
    let pacman = PacMan.new(pellets, walls);
    let score = Score.new(pellets);
    let ghosts = Ghosts.new(pacman, walls, pellets);
    let labels = Labels.new();
	
    do pacman.init();
    do walls.init();
    do pellets.init();
    do score.init();
    do ghosts.init();
    do labels.init();
	
    return;
  }

  method void dispose() {
	do walls.dispose();
	do pellets.dispose();
	do pacman.dispose();
	do score.dispose();
	do ghosts.dispose();
	do labels.dispose();
    do Memory.deAlloc(this);
    return;
  }
}