class Ghost {
  // current position
  field int x, y;
  // size
  field int size;
  // direction 1=up, 2=down, 3=left, 4=right
  field int dir;
  // number of pixels to move at a time
  field int speed;
  // reference to walls collection
  field Walls walls;
  // reference to pac-man
  field PacMan pacman;
  // reference to pellets collection
  field Pellets pellets;

  constructor Ghost new(int xi, int yi, PacMan pm, Walls w, Pellets p) {
    let size = 12;
    let speed = 3;
    let x = xi; // starting x
    let y = yi; // starting y
    let dir = 3; // start going left
    let pacman = pm;
    let walls = w;
    let pellets = p;
    return this;
  }

  method int endX() {
    return x + size - 1;
  }

  method int endY() {
    return y + size - 1;
  }

  method void init() {
    do draw();
    return;
  }

  // advance and draw ghost, check if capturing pacman
  method void next() {
    var boolean didMove;
    let didMove = advance();
    if (didMove) {
      do draw();
    }
    if (pacman.checkOverlapping(x, y, endX(), endY())) {
      do pacman.capture();
    }
    return;
  }

  // move ghost, turn if will hit wall
  method boolean advance() {
    var int nextX, nextY;
    let nextX = getNextX();
    let nextY = getNextY();
    if (willCollideWithWalls(nextX, nextY)) {
      do setNextDirRandom();
      return false;
    }
    if ((x = nextX) & (y = nextY)) {
      return false;
    }
    let x = nextX;
    let y = nextY;
    return true;
  }

  // check if it will collide with a wall
  method boolean willCollideWithWalls(int nextX, int nextY) {
    var int nextEndX, nextEndY;
    let nextEndX = nextX + size - 1;
    let nextEndY = nextY + size - 1;
    return walls.checkOverlapping(nextX, nextY, nextEndX, nextEndY);
  }

  // change direction
  method void setNextDirAI() {
    var int pacmanX, pacmanY;
    var int diffX, diffY;
    let pacmanX = pacman.getX();
    let pacmanY = pacman.getY();
    let diffX = Math.max(x, pacmanX) - Math.min(x, pacmanX);
    let diffY = Math.max(y, pacmanY) - Math.min(y, pacmanY);
    if (~(diffX = 0) & (diffX > diffY)) {
      if (pacmanX < x) {
        let dir = 3;
        return;
      }
      let dir = 4;
      return;
    }
    if (pacmanY < y) {
      let dir = 1;
      return;
    }
    let dir = 2;
    return;
  }
  
  method void setNextDirRandom() {
	  var int i;
	  let i = Random.randRange(150);
    if (i < 25) {
		let dir = 1;
    return;
	  }
	  if (i < 50) {
		  let dir = 2;
      return;
	  }
	  if (i < 75) {
		  let dir = 3;
      return;
	  }
	  if (i < 100) {
		  let dir = 4;
      return;
	  }
	  do setNextDirAI();
    return;
  }
  
  method int getNextX() {
    var int nextX;
    let nextX = x;
    if (dir = 4) {
      let nextX = x + speed;
    }
    if (dir = 3) {
      let nextX = x - speed;
    }
    return nextX;
  }

  method int getNextY() {
    var int nextY;
    let nextY = y;
    if (dir = 2) {
      let nextY = y + speed;
    }
    if (dir = 1) {
      let nextY = y - speed;
    }
	  if (nextY > 220) {
      let nextY = 12;
      do Screen.setColor(false);
      do Screen.drawRectangle(x, y, endX(), endY());
    }
    if (nextY < 12) {
      let nextY = 220;
      do Screen.setColor(false);
      do Screen.drawRectangle(x, y, endX(), endY());
    }
    return nextY;
  }

  method void draw() {
    var int oldLeftX;
    var int oldRightX;
    var int oldTopY;
    var int oldBottomY;
    // remove old ghost
    do Screen.setColor(false);
    // right
    if (dir = 4) {
      let oldLeftX = x - speed;
      let oldRightX = endX() - speed;
      let oldTopY = y;
      let oldBottomY = endY();
    }
    // left
    if (dir = 3) {
      let oldLeftX = x + speed;
      let oldRightX = endX() + speed;
      let oldTopY = y;
      let oldBottomY = endY();
    }
    // down
    if (dir = 2) {
      let oldLeftX = x;
      let oldRightX = endX();
      let oldTopY = y - speed;
      let oldBottomY = endY() - speed;
    }
    // up
    if (dir = 1) {
      let oldLeftX = x;
      let oldRightX = endX();
      let oldTopY = y + speed;
      let oldBottomY = endY() + speed;
    }
    // erase old ghost
    do Screen.drawRectangle(oldLeftX, oldTopY, oldRightX, oldBottomY);
    // fix pellets - redraw pellet if was overlapping one
    do pellets.setCollected(oldLeftX, oldTopY, oldRightX, oldBottomY, false);
    // draw new ghost
    do Screen.setColor(true);
    do Screen.drawRectangle(x, y, endX(), endY());
    return;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}