class Boundary {
  // corners of boundary
  field int leftX, topY, rightX, bottomY;

  // construct boundary
  constructor Boundary new(int x1, int y1, int x2, int y2) {
    let leftX = x1;
    let topY = y1;
    let rightX = x2;
    let bottomY = y2;
    return this;
  }

  // draw boundary rectangle
  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(leftX, topY, rightX, bottomY);
    return;
  }

  // check if object is overlapping boundary given its coordinates
  method boolean checkOverlapping(int x1, int y1, int x2, int y2) {
    return (
      // left of object overlaps right of boundary
      // right of object overlaps left of boundary
      (x1 < (rightX + 1)) & (x2 > (leftX - 1))
    ) & (
      // top of object overlaps bottom of boundary
      // bottom of object overlaps top of boundary
      (y1 < (bottomY + 1)) & (y2 > (topY - 1))
    );
  }

  // dispose boundary
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}